-- programs ------------------------------------------------

entrypoints Program ;

Prog.   	Program ::= [TopDef] ;

FnDef.	   	TopDef ::= Type Ident "(" [Arg] ")" Block ;

ClsDef. 	TopDef ::= "class" Ident "{" [ClsElems] "}" ;

ExtClsDef.	TopDef ::= "class" Ident "extends" Ident "{" [ClsElems] "}" ;

separator nonempty TopDef "" ;

Ar. 	   Arg ::= Type Ident;

separator  Arg "," ;


-- Class elements ------------------------------------------

ClsMethod.	ClsElems ::= Type Ident "(" [Arg] ")" Block ;

ClsField. 	ClsElems ::= Type Ident ";" ;

separator	ClsElems "" ;

-- statements ----------------------------------------------

Blk.       Block ::= "{" [Stmt] "}" ;

separator  Stmt "" ;

Empty.     Stmt ::= ";" ;

BStmt.     Stmt ::= Block ;

Decl.      Stmt ::= Type [Item] ";" ;

NoInit.    Item ::= Ident ; 

Init.      Item ::= Ident "=" Expr ;

separator nonempty Item "," ;

Ass.       Stmt ::= Ident "=" Expr  ";" ;

Incr.      Stmt ::= Ident "++"  ";" ;

Decr.      Stmt ::= Ident "--"  ";" ;

Ret.       Stmt ::= "return" Expr ";" ;

VRet.      Stmt ::= "return" ";" ;

Cond.      Stmt ::= "if" "(" Expr ")" Stmt  ;

CondElse.  Stmt ::= "if" "(" Expr ")" Stmt "else" Stmt  ;

While.     Stmt ::= "while" "(" Expr ")" Stmt ;

For. 	   Stmt ::= "for" "(" Type Ident ":" Ident ")" Stmt ;

SExp.      Stmt ::= Expr  ";" ;

-- Types ---------------------------------------------------

Int.       BasicType ::= "int" ;

Str.       BasicType ::= "string" ;

Bool.      BasicType ::= "boolean" ;

Void.      BasicType ::= "void" ;

internal   Fun. BasicType ::= Type "(" [Type] ")" ;



BType. 		TypeName ::= BasicType ;

ClsType.	TypeName ::= Ident ;


ArrayType. 	Type ::= TypeName "[]" ;

NormalType.	Type ::= TypeName ;

-- ToDO: clsType

-- TODO: Arrays

separator  Type "," ;

-- Expressions ---------------------------------------------


--- todo   Arr[Exp] +
-- TODO:  method . +
-- TODO: attr . +
-- TODO: = new [] +
-- TODO: = new TypeName? +



EAttr.     Expr6 ::= Expr6 "." Ident ;

EMetCall.  Expr6 ::= Expr6 "." Ident "(" [Expr] ")";

ECastNull. Expr6 ::= "(" Type ")" "null" ;

EAt. 	   Expr6 ::= Expr6 "[" Expr "]"  ;

ENewArr.   Expr6 ::= "new" TypeName "[" Expr "]" ;

ENewCls.   Expr6 ::= "new" Ident ;

EVar.      Expr6 ::= Ident ;

ELitInt.   Expr6 ::= Integer ;

ELitTrue.  Expr6 ::= "true" ;

ELitFalse. Expr6 ::= "false" ;

EApp.      Expr6 ::= Ident "(" [Expr] ")" ;

EString.   Expr6 ::= String ;

Neg.       Expr5 ::= "-" Expr6 ;

Not.       Expr5 ::= "!" Expr6 ;

EMul.      Expr4 ::= Expr4 MulOp Expr5 ;

EAdd.      Expr3 ::= Expr3 AddOp Expr4 ;

ERel.      Expr2 ::= Expr2 RelOp Expr3 ;

EAnd.      Expr1 ::= Expr2 "&&" Expr1 ;

EOr.       Expr ::= Expr1 "||" Expr ;

coercions  Expr 6 ;

separator  Expr "," ;

-- operators -----------------------------------------------

Plus.      AddOp ::= "+" ;

Minus.     AddOp ::= "-" ;

Times.     MulOp ::= "*" ;

Div.       MulOp ::= "/" ;

Mod.       MulOp ::= "%" ;

LTH.       RelOp ::= "<" ;

LE.        RelOp ::= "<=" ;

GTH.       RelOp ::= ">" ;

GE.        RelOp ::= ">=" ;

EQU.       RelOp ::= "==" ;

NE.        RelOp ::= "!=" ;

-- comments ------------------------------------------------

comment    "#" ;

comment    "//" ;

comment    "/*" "*/" ;

